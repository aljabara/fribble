* Copyright (c) 2017 dave shields

-include "lib.sbl"

	define('game()players,player,opn,move,i,li,offset,open.word,open.score,n,tiles,moves,li,passed,board') :(game.end)

*	Play a game of fribble. *max* is the maximum number of moves.

*GAME*
game

*	*players* is like g.players, but uses player id's.

	players = array(2)
	players[1] = 1
	players[2] = 2

game.round
	i = 0
game.turn
	pid = players[i = i + 1]				:f(game.round)
	player = g.players[pid]

	g.turn = g.turn + 1
	collect()
	out(ne(g.turnlimit) gt(g.turn,g.turnlimit) 'Game over -- too many turns (' g.turnlimit ').') :s(game.over)
	out(gt(g.turn,g.lastmove + 5) 'Game over - too many turns with no move.') :s(game.over)

	eq(g.trace,g.turn) tracer()

	out(ne(g.maxmoves) gt(g.turn,g.maxmoves) 'Game over -  too many turns.')  :s(game.over)

	out('Move ' g.turn ' for player ' name(player) ', with tiles ' show.tiles(rack(player),'A') '.')
	out(ne(g.showscore) 'B 4 C 4 D 2 F 4 G 3 H 3 J 10 K 5 L 2 M 4 N 2 P 4 Q 10 U 2 V 5 W 4 X 8 Y 3 Z 10 ')

	move = differ(fribble(player)) (eq(g.turn,1) start(pid), find(pid)) :s(game.move)
								:(game.fail)		
game.move
	show.play(makemove(pid,move))

*	Game over if no more tiles.

	out(eq(size(g.tiles)) 'Game over - no more tiles. ')		:s(game.over)
	getrack(pid,move)			:(game.turn)

game.fail

*	Here if no move found.

	out('No move found.')

*	Here if no move found, in which case the return value is a set of swapped tiles. If *move* is null, then pass.

	differ(move)					:s(game.swap)
	passed = passed + 1
	out(gt(passed,6) ' Too many passed moves.')	:s(game.over)
	out('Player ' name(player) ' passes.')		:(game.turn)
	
game.swap
	rack(player) = swap(player,3)
	out('Player ' name(player) ' swapped tiles.')
	out('Player ' name(player) ' now has tiles ' show.tiles(rack(player))) :(game.turn)
game.over						
	endgame()					:(return)
game.end

*	Have fribble play itself.

	&stlimit = -1
	&anchor = &trim = 1

	init()

	g.one = player(1,'Fribble One',1)
	g.two = player(2,'Fribble Two',1)
	g.players[1] = g.one
	g.players[2] = g.two
	total(g.one) = total(g.two) = 0
	rack(g.one) = take(7)
	rack(g.two) = take(7)

	g.turn = g.lastmove = 0
	g.turnlimit = 

*	g.trace = 16
*	eq(g.turn,16) traceon()
	eq(checkwords.count,8285000) traceon()
	game()							:(end)
end
