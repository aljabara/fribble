* Copyright (c) 2017 dave shields

-include "lib.sbl"


	define('turn(pid,mode)')						:(turn.end)

*	Play a turn in a game.

*	Steps are

*	1.	Replenish tiles before making the move if managing the tiles by hand.

*	2.	Find or enter the move to be played.

*	3.	Make the move by updating the data structures, scores, etc.

*	4.	If not managing the tiles by hand, use fribble to update the rack.

turn
	ne(f.trace) out('enter turn pid=' pid)
	player = g.players[pid]

*	Replenish tiles before making the move. This is needed if 
*	managing the player's tiles manually.

	fribble(player)							:s(turn.move)

	rack(player) = replenish(pid)
	ne(f.trace) out('rack pid=' pid '   now ' rack(player))
	

turn.move
	out('turn.move')

*	2.	Find or enter the move to be played.

*	Here to pick the next move.

	out('mode=' mode)
	eq(mode,3)						:s(turn.fribble)
	ident(fribble(player))					:s(turn.move.manual)

turn.fribble

*	Here to have fribble determine the move
	ne(f.trace) out('fribble making move.')

	g.move = find(pid,g.turn)				:s(turn.make)f(turn.fail)

turn.move.manual

	ne(f.trace) out('getting move manually')
*	Here to enter the move manually

turn.manual

turn.manual.in
	out('enter move')
	line = terminal						:f(end)
	move = eval(line)					:f(turn.manual.error)
	out('input: ' show.move(move) ' datatype ' datatype(move) )
	ident(datatype(move),'move')				:s(turn.validate)
	out('need to enter a move')				:(turn.manual)


turn.validate

	ne(f.trace) out('validating move ' show.move(move))
*	See if move is valid, except no test needed for initial move.

	g.move = move
	eq(g.turn,1)						:s(turn.make)
	out('validating gturn=' g.turn)
	valid(g.move)						:s(turn.make)

	out('invalid move - try again')				:(turn.manual.in)

turn.manual.error
	out('eval error ' &errtext)				:(turn.manual)

turn.make

*	3.	Make the move by updating the data structures, scores, etc.


	points(g.move) = scoremove(g.move)
	ne(f.trace) out('making move ' show.move(g.move))
	show.play(makemove(pid,g.move))

turn.replenish

*	4.	Replenish the tiles if managing the tiles automatically.


*	Replenish tiles if necessary. If doing this manually, don't do it here,
*	but at start of next turn.

*	out('replenishing pid= ' pid ' ' show.player(player))
	ne(f.trace) out('replenishing pid= ' pid )
	ident(fribble(player))					:s(turn.done)


*	Here to update rack when fribble is managing the tiles.
*	Remove tiles played on move and then replenish the rack.

	ne(f.trace) out('updating rack pid=' pid)
	rack(player) = less(rack(player), lcase(text(g.move)))
	rack(player) = replenish(pid)			
	ne(f.trace) out('rack pid=' pid '   now ' rack(player))

turn.done
	ne(f.trace) out('turn done')
								:(return)
turn.fail
	out('turn failed - unable to find move.')		:(freturn)
turn.end 

	define('game(mode)players,player,opn,move,i,li,offset,open.word,open.score,n,tiles,moves,li,passed,board') :(game.end)

*	Play a game of fribble. *max* is the maximum number of moves.
*	Mode is one to let you play against fribble, two to let fribble play itself.

*GAME*
game

	pid = 2;* set to two so will become one when loop first entered

game.turn
	pid = (eq(pid,1) 2, 1)
	player = g.players[pid]
	out('g.tilemanager=' g.tilemanager)

	g.turn = g.turn + 1

	out(ne(g.turnlimit) gt(g.turn,g.turnlimit) 
.			'Game over -- too many turns (' g.turnlimit ').') :s(game.over)
	out(gt(g.turn,g.lastmove + 5) 
.			'Game over - too many turns with no move.') :s(game.over)
	out(ne(g.maxmoves) gt(g.turn,g.maxmoves) 
.			'Game over -  too many turns.')  :s(game.over)

	eq(g.trace,g.turn) traceon()


	out('Move ' g.turn ' for player ' name(player) 
.		 (differ(g.tilemanager) ', with tiles ' show.tiles(rack(player),'A'),'')	'.')
	out(ne(g.showscore) 'B 4 C 4 D 2 F 4 G 3 H 3 J 10 K 5 L 2 M 4 N 2 P 4 Q 10 U 2 V 5 W 4 X 8 Y 3 Z 10 ')
	out('B 4 C 4 D 2 F 4 G 3 H 3 J 10 K 5 L 2 M 4 N 2 P 4 Q 10 U 2 V 5 W 4 X 8 Y 3 Z 10 ')


	turn(pid,mode)					:s(game.turn)

*	Here if turn failed because no move could be found. Try to swap three tiles

	out('no move, swapping tiles.')
	rack(player) = swap(pid,3)			:(game.turn)
	
game.fail

*	Here if no move found.

	out('No move found.')

*	Here if no move found, in which case the return value is a set of swapped tiles. If *move* is null, then pass.

	differ(move)					:s(game.swap)
	passed = passed + 1
	out(gt(passed,6) ' Too many passed moves.')	:s(game.over)
	out('Player ' name(player) ' passes.')		:(game.turn)
	
game.swap
	out('swapping tiles pid=' pid)
	rack(player) = swap(pid,3)
	out('Player ' name(player) ' swapped tiles.')
	out('Player ' name(player) ' now has tiles ' show.tiles(rack(player))) :(game.turn)
game.over						
	endgame()					:(return)
game.end

*	Have fribble play itself.

	&stlimit = -1
	&anchor = &trim = 1

	init()


*	The program can now run in two modes.
*	f.mode = 1 for solo mode, where you play against fribble.
*	f.mode = 2 for self mode, where fribble moves for both players.
*	f.mode = 3 for match mode, where you use fribble to play another player.
	
	f.mode = 1
	f.mode = 2
	f.mode = 3


	options = host(0) ' '
*	output = 'host <' options '>'

options.loop
	options break(' ') . opt ' ' =			:f(options.loop.done)
	output = 'option ' opt
	f.mode = ident(opt,'self') 2			:s(options.loop)
	f.mode = ident(opt,'solo') 1			:s(options.loop)
	f.mode = ident(opt,'foe')  3
	output = 'unknown option ' opt
							:(options.loop)
options.loop.done


	out('fribble mode=' f.mode)

	eq(f.mode,1)					:s(setup.1)
	eq(f.mode,2)					:s(setup.2)
	eq(f.mode,3)					:s(setup.3)

setup.1

*	Setup for mode 1

	g.one = player(1,'You')
	g.two = player(2,'Fribble',1)
	g.tilemanager = 1
							:(setup.done)
setup.2

*	Setup for mode 2
	g.one = player(1,'Fribble I',1)
	g.two = player(2,'Fribble II',1)
	g.tilemanager = 1
							:(setup.done)

setup.3

*	Setup for mode 3

	g.one = player(1,'You')
	g.two = player(2,'Foe')
	g.tilemanager 
							:(setup.done)
setup.done

	differ(g.tilemanager) shuffle()

	g.players[1] = g.one; g.players[2] = g.two

*	Program manages the racks.

	rack(g.one) = take(7)
	rack(g.two) = take(7)

	g.turn = g.lastmove = total(g.one) = total(g.two) = 0
	g.turnlimit = 
	
*	Set f.trace to trace fribble.

	f.trace =

	game(f.mode)							:(end)
end
